#!/usr/bin/perl

use 5.030 ; use warnings ; 
use Getopt::Std ; getopts '2:d:Fm:o:s:t:T:' , \my%o ; 
use Term::ANSIColor qw [ :constants ] ; $Term::ANSIColor::AUTORESET = 1 ;
use List::Util qw[ min ] ; 
use POSIX qw [ floor ceil ] ;
#use Math::Random::MT qw[ rand srand ] ; # Florent Angly 氏が作成。
use Math::Random::MT::Auto qw[ rand srand ] ; # Jerry D. Hedden 氏が作成。
use Time::HiRes qw[ gettimeofday tv_interval ]; 
my $t0 = [gettimeofday] ;

if ( exists $o{T} ) { 
  my $cmd ; 
  do { 
    no warnings 'experimental::smartmatch' ; 
    $cmd = "$0 -d1 -F 19%" when '19%' ; 
    $cmd = "$0 -d1 -F 19%" when '.19' ; 
    $cmd = "$0 -d1 -F 19%" when '20%' ; 
    $cmd = "$0 -t 3e4 -s1" when 's1'  ; # シードを固定。Math::Random::MT qw[ rand srand ] で最後の出力は 4357 となる。
  } for $o{T} ;
  system $cmd ; 
  exit ;
}


my $seed = exists $o{s} ? srand $o{s} : srand ;
END{ 
  exit if exists $o{T} ;
  exit if 0 eq ($o{2}//'') ; 
  say STDERR RED BOLD FAINT "Random seed = $seed" if ! $o{F} ;
  say STDERR RED BOLD FAINT "The elapsed second = " .tv_interval ($t0) ;
}

$o{d} //= 1 ; # 10 ** $o{d} の数が最大
$o{m} //= 3 ; # 何個の異なる数を取り出すか。ただし0より大。
$o{t} //= 200 ; # 各分母で、何回ランダムに試すか? 
$o{o} //= 1 ; 
$ARGV[0] //= '99.99999999%' ;
my $uFrac = $ARGV[0] =~ s/%$// ? "$ARGV[0]" * "0.01" : $ARGV[0] ;
my $u10 = 10 ** $o{d} ; 
my @f2d ; #  keys %{ $f2d{ $fig } }  == [ denom1, denom2, denom3 .. ]のようになる

# ↑小数点以下$o{d}桁の fig という数の並び(000-999)に対して、次を格納。
# {denom | あるN/denom を小数点以下 $o{d}まで四捨五入した結果 が fig に一致} 

for my $den ( 1 .. $u10 ) {  # den ← denominator
  $SIG{CONT} = sub { say STDERR BOLD YELLOW "Denominator = $den" } ;
  # その分母が小数点以下d桁でどんなfigureを生成するかを、次の2行で求める。
  my @figs = () ;
  push @figs , int 0.5 + $u10 * $_ / $den for 1 .. ($den * $uFrac ) ; # $den-1
  goto F if $o{F} ;
  my %freq ; 
  print "$den\t" ; # 1. 分母を出力
  for my $i ( 0 .. $o{t} - 1 ) { # $o{t}回の反復
    my @cand = & pickR ( \@figs ) ; # ここで$o{m}個(足りない場合は全部)を@figsからランダムに取り出す。
    my $Nth = $o{o} ; 
    if ( @cand ) { 
      my %comm ;#  = map {($_,1)} $f2d [ shift @cand ]  ; ## 
      $comm{$_} = 1 for keys %{ $f2d [ shift @cand ] } ;
      for my $c ( @cand ) { grep { ! exists $f2d[$c]{$_} && delete $comm{$_} } keys %comm } ;
      $Nth += keys %comm ;
    }
    $freq{$Nth} ++ ; # 頻度表に集計である。
    
  }
  say join "\t" , map { ${_} , $freq{$_} } sort { $a <=> $b } keys %freq ; 
  F: $f2d [ $_ ] { $den } = 1 for @figs ; # <-- 変数の使い方は、これが分かりやすい。
}

#do{ say join "\t", sprintf(".%0$o{d}d",$_), sort{$a<=>$b} keys %{$f2d[$_]} for 1..$u10/2 } if $o{F} ; 
do{ say join "\t", sprintf("%.$o{d}f",$_/$u10), sort{$a<=>$b} keys %{$f2d[$_]} for 1..$#f2d } if $o{F} ; 


exit ;

sub pickR ( $ ){ ## $den-1くらいの長さの配列を@candにコピーは重いので、ダステンフェルドの手法に書き換えたい。
  my @cand = @{ $_[0] } ;
  my @ret ; # = () ; 
  push @ret , splice @cand , rand( scalar @cand ) , 1 for 1 .. min $o{m} , scalar @cand ; 
  return @ret ;
}

sub VERSION_MESSAGE {}
sub HELP_MESSAGE {
  use FindBin qw[ $Script ] ;
  $ARGV[1] //= '' ;
  open my $FH , '<' , $0 ;
  while(<$FH>){
    s/\$0/$Script/g ;
    print $_ if $ARGV[1] eq 'opt' ? m/^\ +\-/ : s/^=head1// .. s/^=cut// ;
  }
  close $FH ;
  exit 0 ;
}


=encoding utf8

=head1

  $0 [ -d 1 ] [-m 3] [-t 200] [-o 1] [-F] 引数

   各整数分母に対して、分数を-dで指定される小数点以下の桁数で四捨五入で現した時に、
   それらの数を (同時に) -mで指定される個数取り出した場合に、
   元の分母が小さい順に何番目の候補として現れるかを、
   -t で指定される回数だけ反復するモンテカルロ法で頻度集計する。
   各整数分母は1から始まり、-dで指定される桁数以内の整数である(最大は9..9である)。

   引数は分数の最大値を指定する。指定しない場合は 99.99999999% (9が8個) となる。


  オプション: 

  -d N : 1から10**d まで調べることになる。正の整数。(digit)
  -m N : 1〜 10**d の各分母Dに対して、1からD-1の中から取れる限りN個の整数を取り出して、それぞれを分子とする。未指定だと3。(multitude?)
  -t N : 各分子に対して、モンテカルロ法を N 回適用する。未指定だと200(trial)
  -o N : 未指定だと1。「Dに対して、Dを逆算した場合、候補の中からDが何番目に小さな数であるか」の数え方は通常は1始まりだが。0始まりにもできる。(offset)
  -s N : ランダムシードの指定。

  -F   : 小数点以下d桁の数に対して、ありうる分数の一覧を出す。(プログラムの正しさの点検のためでもある。)

  計算時間がかかる時の為に、HUPシグナルで、どの分母まで処理したのかを標準エラー出力に(黄色で)表示する。
  このことにより Ctrl + Z で一時停止して、fg または bg のコマンドで再開すると、分母などの情報が出力される。
  kill -s CONT %1 とか kill -s CONT 123445 のようなコマンドで走らせながら、分母情報を表示が可能。
 
  使用例: 
    $0 -d1 # 簡単に。

  開発メモ: 
    * -@ で特定の分母の値またはその集合だけ、出力したい。

=cut
